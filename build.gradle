apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'jacoco'

buildscript {
    ext {
        kotlinVersion = '1.3.31'
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

group 'com.github.seed'
version '1.0.0-SNAPSHOT'

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude:  [
                    'com/github/ExcludeMe*.*'
            ])
        })
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude:  [
                    'com/github/ExcludeMe*.*'
            ])
        })
    }
    violationRules {
        rule {
            limit {
                minimum = 0.95
            }
        }
    }
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    implementation('org.jetbrains.kotlin:kotlin-reflect')

    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.4.2')
    testImplementation("io.kotlintest:kotlintest-runner-junit5:3.3.2")
    testImplementation("io.mockk:mockk:1.9.3")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    finalizedBy jacocoTestCoverageVerification
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}
